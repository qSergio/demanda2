model
{

#Likelihood
for (i in 1:n) {

	y[i] ~ dnorm(mu[i],tau)

	mu[i]<-alpha+beta[ano[i]]+gama[sem[i]]+epsilon[tem[i]]+bega[ano[i],sem[i]]+beep[ano[i],tem[i]]+gaep[sem[i],tem[i]]

	}

#Priors

alpha ~ dnorm(0,0.001)

for (i in 1:2) { 

	beta[i] ~ dnorm(0,0.001) 

	for (j in 1:26) { bega[i,j] ~ dnorm(0,0.001) }

	for (j in 1:2) { beep[i,j] ~ dnorm(0,0.001) }
	
}

for (i in 1:26) { 

	gama[i] ~ dnorm(0,0.001) 

	for (j in 1:2) { gaep[i,j] ~ dnorm(0,0.001) }

	}

for (i in 1:2) { epsilon[i] ~ dnorm(0,0.001) }

tau ~ dgamma(0.001,0.001)

#Restrictions for interpretability

alpha.est <- alpha+mean(beta[])+mean(gama[])+mean(epsilon[])+mean(bega[,])+mean(beep[,])+mean(gaep[,])

for (i in 1:2) { 

	beta.est[i] <- beta[i]-mean(beta[])+mean(bega[i,])-mean(bega[,])+mean(beep[i,])-mean(beep[,])

	for (j in 1:26) { bega.est[i,j] <- bega[i,j]-mean(bega[i,])-mean(bega[,j])+mean(bega[,]) }

	for (j in 1:2) { beep.est[i,j] <- beep[i,j]-mean(beep[i,])-mean(beep[,j])+mean(beep[,]) }

	}

for (i in 1:26) { 

	gama.est[i] <- gama[i]-mean(gama[])+mean(bega[,i])-mean(bega[,])+mean(gaep[i,])-mean(gaep[,])

	for (j in 1:2) { gaep.est[i,j] <- gaep[i,j]-mean(gaep[i,])-mean(gaep[,j])+mean(gaep[,]) }

	}

for (i in 1:2) { epsilon.est[i] <- epsilon[i]-mean(epsilon[])+mean(beep[,i])-mean(beep[,])+mean(gaep[,i])-mean(gaep[,]) }

#Prediction

for (i in 1:n) { yf[i] ~ dnorm(mu[i],tau) }

for (j in 1:52) {

	#mup[j] <- alpha+mean(beta[])+gama[sem[j]]+epsilon[tem[j]]+mean(bega[,sem[j]])+mean(beep[,tem[j]])+gaep[sem[j],tem[j]]

	mup[j] <- alpha.est+beta.est[2]-beta.est[1]+gama.est[sem[j]]+epsilon.est[tem[j]]+mean(bega.est[,sem[j]])+mean(beep.est[,tem[j]])+gaep.est[sem[j],tem[j]]

	yp[j] ~ dnorm(mup[j],tau)

	}

}
